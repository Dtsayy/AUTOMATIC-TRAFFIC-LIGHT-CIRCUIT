;CHUONG TRINH DEN GIAO THONG
;KET VDK VOI CAC COT DEN BANG DU LIEU NOI TIEP 
;MOI COT DEN VOI 2 THANH GHI DICH 74595
;DAT1 BIT P2.0 ; CHAN DU LIEU 1
;DAT2 BIT P2.1 ; CHAN DU LIEU 2
;DA3 BIT P2.2 ; CHAN DU LIEU 3 CHO LED HIEN THI KHI CHINH
;CLOCK BIT P2.3 ;CHAN TAO XUNG CLOCK
;ST BIT P2.4 ; CHAT XUAT DU LIEU RA BO DEM TRONG IC74595
;OE BIT P2.5 ;CHAN CHO PHEP HIEN THI

DD1 BIT P3.5
DX1 BIT P3.3
DV1 BIT P3.4

DD2 BIT P3.2
DX2 BIT P1.5
DV2 BIT P1.7

LM BIT P2.0
LC BIT P2.1
LD BIT P2.2
LL BIT P2.3

LB BIT P0.0

PL EQU P0

BT BIT P1.3;
MODE BIT P1.0 ; CHON CHE DO DIEU CHINH
TANG BIT P1.2 ; TANG
GIAM BIT P1.1 ; GIAM
LED_CHINH BIT 20H.1 ; LED HIEN THI KHI CHINH CHE DO
BIT_GIAY BIT 20H.2 ; LED HIEN THI KHI CHINH CHE DO
TG1 EQU R7 ; THOI GIAN 1
TG2 EQU R6 ; THOI GIAN 2
PN BIT 20H.0 ; BIT NHO CO PHIM NHAN
CHE_DO EQU R4 ;  CHE DO CHAY GOM 3 CHE DO CAI DAT DUOC, CHE DO = 0 DEN VANG CHOP TAT, CHE DO = 1, CHE DO = 2 DIEU CHINH DUOC THOI GIAN
CHINH EQU R5 ; CHINH GIA TRI, 
;CHINH = 1 CHINH CHE DO,
;NEU CHE DO KHAC 0 
;CHINH = 2 CHINH THOI GIAN DEN DO COT 1, CHINH = 3 CHINH THOI GIAN DEN XANH COT 1, CHINH = 4 CHINH DEN VANG COT 1, CHINH = 5 CHINH DEN VANG COT 2
DEN_CHINH EQU 21H ; TRANG THAI DEN KHI CHINH THOI GIAN

TGD11 EQU 2EH ; THOI GIAN DEN DO COT 1 CHE DO 1
TGX11 EQU 2DH ; THOI GIAN DEN VANG COT 1 CHE DO 1
TGV11 EQU 2CH ; THOI GIAN DEN VANG COT 1 CHE DO 1

TGD21 EQU 2BH ; THOI GIAN DEN DO COT 2 CHE DO 1
TGX21 EQU 2AH ; THOI GIAN DEN XANH COT 2 CHE DO 1
TGV21 EQU 29H ; THOI GIAN DEN VANG COT 2 CHE DO 1

TGD12 EQU 28H ; THOI GIAN DEN DO COT 1 CHE DO 2
TGX12 EQU 27H ; THOI GIAN DEN DO COT 1 CHE DO 2
TGV12 EQU 26H ; THOI GIAN DEN DO COT 1 CHE DO 2

TGD22 EQU 25H ; DEN XANH 2 = TONG TG COT 1 - DEN DO 2 - DEN VANG 2
TGX22 EQU 24H ; DEN DO 2 = DEN XANH 1 + DEN VANG 1
TGV22 EQU 23H ; THOI GIAN DEN VANG CHO COT 2 CHE DO 2

TG_CHINH EQU 22H ; THOI GIAN HIEN THI KHI CHINH
;======================================== 
	ORG	0 ; DIEM NHAP RESET
MAIN: ; BAT DAU CHUONG TRINH
	CLR DD1
	CLR DD2 
	CLR DV1
	CLR DV2
	CLR DX1
	CLR DX2
	MOV R1,#250
ON_DINH:
	CALL DELAY;
	DJNZ R1,ON_DINH
	MOV CHINH,#0
	MOV SP,#30H ; NAP VI TRI NGAN XEP BAT DAU TU 31H
	MOV DPTR,#MA7DOAN ; NAP CON TRO DU LIEU VUNG MA 7 DOAN
	MOV TG_CHINH,#0 ; NAP THOI GIAN CHINH = 0  
	MOV DEN_CHINH,#0FFH ; TAT CAC DEN KHI CHINH
	CALL DOC_TG ; DOC THOI GIAN DUOC LUU TRUOC DO TRONG EEPROM
	CJNE A,#0AAH,MP1 ; KIEM TRA MA DA GHI TRUOC DO
	JMP MAIN1 ; NHAY DEN MAIN KHI THOI GIA CU DA DUOC GHI
MP1: ; KHI TRONG EEPROM CHUA CO GIA TRI THOI GIAN
	MOV CHE_DO,#1 ; NAP CHEO DO BAN DAU = 1
	MOV TGD11,#30 ; NAP GIA TRI MAC DINH CHO THOI GIAN
	MOV TGX11,#25
	MOV TGV11,#8
	MOV TGV21,#8

	MOV TGD12,#50
	MOV TGX12,#40
	MOV TGV12,#5
	MOV TGV22,#5
	CALL NAPTG ; NAP THOI GIAN VAO EEPROM DE LUU TRU THOI GIAN VA CHE DO KHI MAT DIEN
MAIN1:
	CALL TINH_TG ; TINH THOI GIAN CHO CAC COT DEN
	MOV	8,CHE_DO ; SUAT HIEN THI CHE DO HIEN TAI RA LED 7 DOAN
	SETB LED_CHINH
CHUONG_TRINH_1: ; CHUONG TRINH 1 CHE DO BAN DEM, 2 DEN VANG CHOP TAT
	CJNE CHE_DO,#0,CHUONG_TRINH_2
		MOV TG1,#0 ; NAP THOI GIAN = 0
		MOV TG2,#0
		CALL DOIMA 
		CALL BAT_VANG2
		MOV 62H,#11111111B ; BAT DEN VANG COT 2
		CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
		CALL BAT_VANG1
		MOV 65H,#11111111B ; BAT DEN VANG CON 1
		CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
		JMP CHUONG_TRINH_1 ; LAP LAI CHUONG TRINH
;--------------------------------------- 		
CHUONG_TRINH_2:
	CJNE CHE_DO,#1,CHUONG_TRINH_3
	MOV TG1,TGD11 ; DAT THOI GIAN DEN DO COT DEN 1
	MOV TG2,TGX21 ; DAT THOI GIAN DEN XANH COT DEN 2
	CALL BAT_DO1
M1:
	CALL DOIMA ; GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ; GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1 ; GIAM THOI GIAN
	DEC TG2 ; GIAM THOI GIAN
	CJNE TG2,#0,M1 ; LAP LAI TOI KHI THOI GIAN COT DEN 2 = 0
	MOV TG2,TGV21 ; NAP LAI THOI GIAN DEN VANG CHO COT DEN 2 
	CALL BAT_VANG2
M2:
	CALL DOIMA ; GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ; GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1
	DEC TG2
	CJNE TG2,#0,M2 ; LAP LAI TOI KHI THOI GIAN COT DEN 2 = 0
	MOV TG1,TGX11 ; DAT THOI GIAN COT DEN 1 15 GIAY
	MOV TG2,TGD21 ; DAT THOI GIAN COT DEN 2 20 GIAY
	CALL BAT_XANH1
M3:
	CALL DOIMA ; GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ; GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1
	DEC TG2
	CJNE TG1,#0,M3 ; LAP LAI TOI KHI THOI GIAN COT DEN 1 = 0
	MOV TG1,TGV11 ; DAT THOI GIAN DEN VANG COT DEN 1
	CALL BAT_VANG1
M4:	
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1
	DEC TG2
	CJNE TG1,#0,M4 ;LAP LAI TOI KHI THOI GIAN COT DEN 1 = 0
	JMP CHUONG_TRINH_2 ;LAP LAI CHUONG TRINH
;--------------------------------------- 
CHUONG_TRINH_3:
	CJNE CHE_DO,#2,CHUONG_TRINH_4
	MOV TG1,TGD12 ; DAT THOI GIAN DEN DO COT DEN 1
	MOV TG2,TGX22 ; DAT THOI GIAN DEN XANH COT DEN 2
	CALL BAT_DO1
CT31:
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1
	DEC TG2
	CJNE TG2,#0,CT31 ;LAP LAI TOI KHI THOI GIAN COT DEN 2 = 0
	MOV TG2,TGV22 ; NAP LAI THOI GIAN CHO COT DEN 2 
	CALL BAT_VANG2
CT32:
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1
	DEC TG2
	CJNE TG2,#0,CT32 ;LAP LAI TOI KHI THOI GIAN COT DEN 2 = 0
	MOV TG1,TGX12 ; DAT THOI GIAN DEN XANH COT 1 
	MOV TG2,TGD22 ; DAT THOI GIAN DEN DO COT 2
	CALL BAT_XANH1
CT33:
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1
	DEC TG2
	CJNE TG1,#0,CT33 ;LAP LAI TOI KHI THOI GIAN COT DEN 1 = 0
	MOV TG1,TGV12 ; DAT THOI GIAN DEN VANG COT DEN 1
	CALL BAT_VANG1
CT34:	
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	DEC TG1
	DEC TG2
	CJNE TG1,#0,CT34 ;LAP LAI TOI KHI THOI GIAN COT DEN 1 = 0
	JMP CHUONG_TRINH_3 ;LAP LAI CHUONG TRINH
;---------------------------------------- 
CHUONG_TRINH_4:
	CJNE CHE_DO,#3,CHUONG_TRINH_5
	MOV TG1,#0 ; DAT THOI GIAN DEN DO COT DEN 1
	MOV TG2,#0 ; DAT THOI GIAN DEN XANH COT DEN 2
	CALL BAT_DO1
CT41:
	CJNE CHE_DO,#3,CHUONG_TRINH_5
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	JB BT,CT41
	CALL BAT_VANG2
CT42:
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	MOV TG1,#0 ; DAT THOI GIAN DEN XANH COT 1 
	MOV TG2,#0 ; DAT THOI GIAN DEN DO COT 2
	CALL BAT_XANH1
CT43:
	CJNE CHE_DO,#3,CHUONG_TRINH_5
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	JB BT,CT43
	CALL BAT_VANG1
CT44:	
	CALL DOIMA ;GOI CHUONG TRINH DOI MA BCD SANG MA 7 DOAN
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	CALL TRE ;GOI CHUONG TRINH TAO TRE 1 GIAY
	MOV TG1,#0 ; DAT THOI GIAN DEN XANH COT 1 
	MOV TG2,#0 ; DAT THOI GIAN DEN DO COT 2
	JMP CHUONG_TRINH_4 ;LAP LAI CHUONG TRINH
CHUONG_TRINH_5:
	JMP CHUONG_TRINH_1
;======================================== 
BAT_DO1:
	MOV 62H,#01101111B ; BAT COT DEN DO COT 1, DEN XANH CHO NGUOI DI BO
	MOV 65H,#10110111B ; BAT DEN XANH CHO COT 2, DEN DO CHO NGUOI DI BO
	CLR DX1
	CLR DV1
	SETB DD1
	CLR DD2
	CLR DV2
	SETB DX2
	RET
;======================================== 
BAT_XANH1:
	MOV 62H,#10110111B ; CHUYEN DEN XANH CHO COT 1, BAT DEN DO CHO NGUOI DI BO
	MOV 65H,#01101111B ;CHUYEN DEN DO CHO COT 2, DEN XANH CHO NGUOI DI BO
	CLR DD1
	CLR DV1
	SETB DX1
	CLR DX2
	CLR DV2
	SETB DD2
	RET
;======================================== 
BAT_VANG1:
	MOV 62H,#11011111B ;CHUYEN DEN VANG CHO COT 1, TAT DEN CHO NGUOI DI BO
	CLR DD1
	CLR DX1
	SETB DV1
	CLR DV2
	CLR DX2
	RET
;======================================== 
BAT_VANG2:
	MOV 65H,#11011111B ;CHUYEN DEN VANG CHO COT 1, TAT DEN CHO NGUOI DI BO
	CLR DD2
	CLR DX2
	SETB DV2
	CLR DV1
	CLR DX1
	RET
;======================================== 
TINH_TG: ; TINH THOI GIAN 
	PUSH ACC ; CAT TAM GIA TRI THANH GHI A VAO NGAN XEP 
	MOV A,TGX11 ; NAP A THOI GIAN DEN XANH COT 1
	ADD A,TGV11 ; CONG THOI GIAN DEN XANH COT 1 VOI THOI GIAN DEN VANG COT 1
	MOV TGD21,A ; THOI GIAN DEN DO COT 2 BANG TONG THOI GIAN DEN XANH + DEN VANG COT 1
	ADD A,TGD11 ; TINH TONG THOI GIAN CHO COT 1
	CLR C ; XOA CO C CHO PHE TRU KHONG MUON
	SUBB A,TGD21 ; TRU THOI GIAN DEN DO COT 2 
	CLR C
	SUBB A,TGV21 ; TRU THOI GIAN DEN VANG COT 2
	MOV TGX21,A ; NAP THOI GIAN DEN XANH COT 2 = TONG THOI GIAN DEN COT 1 - DEN THOI GIAN DEN DO COT 2 - THOI GIAN DEN VANG COT 2
	
	MOV A,TGX12
	ADD A,TGV12
	MOV TGD22,A
	ADD A,TGD12 ; TINH TONG THOI GIAN CHO COT 1
	CLR C
	SUBB A,TGD22 ; TRU TONG THOI GIAN COT 1 - TG DEN DO COT 2
	CLR C
	SUBB A,TGV22
	MOV TGX22,A ; NAP THOI GIAN DEN XANH COT 2
	POP ACC ; LAY LAI GIA TRI CHO THANH GHI A TU NGAN XEP 
	RET ; TROAT KHOI CHUONG TRINH CON 
;======================================== 
;CHUONG TRINH DOI MA NHI PHAN SANG MA 7 DOAN
DOIMA:						
	PUSH ACC ; CAT TAM GIA TRI THANH GHI A VAO NGAN XEP 
	MOV A,TG1 ;DUA THOI GIAN 1 VAO A
	MOV B,#10
	DIV AB
	MOVC A,@A+DPTR ;LAY MA THEO VI TRI
	MOV 60H,A ;LUU MA 7 DOAN HANG CHUC THOI GIAN 1 VAO 60H
	MOV A,B
	MOVC A,@A+DPTR ;LAY MA THEO VI TRI
	MOV	61H,A ;LUU MA 7 DOAN HANG DON VI THOI GIAN 1 VAO 61H

	MOV A,TG2 ;DUA THOI GIAN 2 VAO A
	MOV B,#10
	DIV AB
	MOVC A,@A+DPTR ;LAY MA THEO VI TRI
	MOV 63H,A ;LUU MA 7 DOAN HANG CHUC THOI GIAN 2 VAO 63H
	MOV A,B
	MOVC A,@A+DPTR ;LAY MA THEO VI TRI
	MOV	64H,A ;LUU MA 7 DOAN HANG DON VI THOI GIAN 2 VAO 64H
	
	MOV A,TG_CHINH ;DUA THOI GIAN 2 VAO A
	MOV B,#10
	DIV AB
	MOVC A,@A+DPTR ;LAY MA THEO VI TRI
	MOV 67H,A ;LUU MA 7 DOAN HANG CHUC THOI GIAN 2 VAO 63H
	MOV A,B
	MOVC A,@A+DPTR ;LAY MA THEO VI TRI
	MOV	66H,A ;LUU MA 7 DOAN HANG DON VI THOI GIAN 2 VAO 64H
	MOV A,8
	MOVC A,@A+DPTR ;LAY MA THEO VI TRI
	MOV	69H,A ;LUU MA 7 DOAN HANG DON VI THOI GIAN 2 VAO 64H
	POP ACC ; LAY LAI GIA TRI CHO THANH GHI A TU NGAN XEP 
	RET ;THOAT KHOI CHUONG TRINH CON
;======================================== 
HIENTHI: ; CHUONG TRINH QUET LED HIEN THI THOI GAN VÀ TRANG THÁI DEN
	MOV PL,67H
	CLR LC
	CALL DELAY ;TAO TRE CO LED
	SETB LC
	MOV PL,66H
	MOV C,BIT_GIAY
	MOV LB,C
	CLR LD
	CALL DELAY ;TAO TRE CO LED
	SETB LD
	MOV PL,DEN_CHINH
	CLR LL
	CALL DELAY ;TAO TRE CO LED
	SETB LL
	MOV PL,69H
	MOV C,LED_CHINH
	MOV LB,C
	CLR LM
	CALL DELAY ;TAO TRE CO LED
	SETB LM
	RET
;============================
;CHUONG TRINH CON TAO THOI GIAN TRE CHO LED
DELAY:	
	PUSH 0
	PUSH 1
	MOV R1,#5
DEL:
	MOV R0,#250
	DJNZ R0,$
	DJNZ R1,DEL
	POP	1
	POP	0
	RET
;======================================== 
QUET_PHIM_NHAN: ; QUET NUT NHAN
	PUSH ACC
	JNB PN,NKN ; KIEM TRA KHONG NHAN NUT KHI PN = 0
		JB TANG,QPN1
			CLR PN
			CALL NHAN_TANG
			JMP TQPN
	QPN1:
		JB GIAM,QPN2
			CLR PN
			CALL NHAN_GIAM
			JMP TQPN
	QPN2:
		JB MODE,TQPN
			CLR PN
			CALL NHAN_MODE
			JMP TQPN
NKN: ; KIEM TRA NUT KHONG NHAN
	JNB TANG,TQPN
	JNB GIAM,TQPN
	JNB MODE,TQPN
		SETB PN	; PN = 1 ; CHO PHEP NHAN PHIM TIEPP THEO
		CALL DOIMA
TQPN: ; THOAT QUET PHIM NHAN
	POP ACC
	RET
;========================================
NHAN_GIAM: ; KHI NHAN NUT GIAM
	PUSH ACC ; CAT TAM GIA TRI THANH GHI A VAO NGAN XEP 
	CJNE CHINH,#1,NG1
		DEC CHE_DO ; GIAM CHE DO
		CJNE CHE_DO,#255,NG0 ; GIOI HAN TU 0-2
		MOV CHE_DO,#3
	NG0:
		MOV	8,CHE_DO
		JMP TNG ; NHAY TOI THOAT NHAN GIAM
NG1:
	CJNE CHINH,#2,NG2 ; KHI CHINH = 2
		CJNE CHE_DO,#1,NG11 ; KIEM TRA CHE DO
			DEC TGD11 ; GIAM THOI GIAN DEN DO COT 1 CHO CHE DO TUONG UNG KHI CHE DO = 1
			MOV A,TGD11
			CJNE A,#4,NG10
				MOV TGD11,#90 ; GIOI HAN TU 10 -  90
		NG10:
			MOV TG_CHINH,TGD11
			JMP TNG1
	NG11:
		CJNE CHE_DO,#2,TNG1
			DEC TGD12
			MOV A,TGD12
			CJNE A,#4,NG13
				MOV TGD12,#90
		NG13:
			MOV TG_CHINH,TGD12
	TNG1:
		CALL TINH_TG
		JMP TNG
;--------------------------------------- 
NG2:
	CJNE CHINH,#3,NG3
		CJNE CHE_DO,#1,NG21
			DEC TGX11
			MOV A,TGX11
			CJNE A,#4,NG20
				MOV TGX11,#90
		NG20:
			MOV TG_CHINH,TGX11
			JMP TNG2
	NG21:
		CJNE CHE_DO,#2,TNG2
			DEC TGX12
			MOV A,TGX12
			CJNE A,#4,NG23
				MOV TGX12,#90
		NG23:
			MOV TG_CHINH,TGX12
	TNG2:
		CALL TINH_TG
		JMP TNG
;--------------------------------------- 
NG3:
	CJNE CHINH,#4,NG4
		CJNE CHE_DO,#1,NG31
			DEC TGV11
			MOV A,TGV11
			CJNE A,#0,NG30
				MOV TGV11,#15
		NG30:
			MOV TG_CHINH,TGV11
			JMP TNG3
	NG31:
		CJNE CHE_DO,#2,TNG3
			DEC TGV12
			MOV A,TGV12
			CJNE A,#0,NG33
				MOV TGV12,#15
		NG33:
			MOV TG_CHINH,TGV12
	TNG3:
		CALL TINH_TG
		JMP TNG
;--------------------------------------- 
NG4:
	CJNE CHINH,#5,TNG
		CJNE CHE_DO,#1,NG41
			DEC TGV21
			MOV A,TGV21
			CJNE A,#0,NG40
				MOV TGV21,#15
		NG40:
			MOV TG_CHINH,TGV21
			JMP TNG3
	NG41:
		CJNE CHE_DO,#2,TNG4
			DEC TGV22
			MOV A,TGV22
			CJNE A,#0,NG43
				MOV TGV22,#15
		NG43:
			MOV TG_CHINH,TGV22
	TNG4:
		CALL TINH_TG
		JMP TNG
TNG:
	POP ACC ; LAY LAI GIA TRI CHO THANH GHI A TU NGAN XEP 
	RET
;======================================== 
NHAN_TANG: ; XU LY KHI NHAN TANG
	PUSH ACC ; CAT TAM GIA TRI THANH GHI A VAO NGAN XEP 
	CJNE CHINH,#1,NT1
		INC CHE_DO
		CJNE CHE_DO,#4,NT0
		MOV CHE_DO,#0
	NT0:
		MOV	8,CHE_DO
		JMP TNT
NT1:
	CJNE CHINH,#2,NT2
		CJNE CHE_DO,#1,NT11
			INC TGD11
			MOV A,TGD11
			CJNE A,#91,NT10
				MOV TGD11,#5
		NT10:
			MOV TG_CHINH,TGD11
			JMP TNT1
	NT11:
		CJNE CHE_DO,#2,TNT1
			INC TGD12
			MOV A,TGD12
			CJNE A,#91,NT13
				MOV TGD12,#5
		NT13:
			MOV TG_CHINH,TGD12
	TNT1:
		CALL TINH_TG
		JMP TNT
;--------------------------------------- 
NT2:
	CJNE CHINH,#3,NT3
		CJNE CHE_DO,#1,NT21
			INC TGX11
			MOV A,TGX11
			CJNE A,#91,NT20
				MOV TGX11,#5
		NT20:
			MOV TG_CHINH,TGX11
			JMP TNT2
	NT21:
		CJNE CHE_DO,#2,TNT2
			INC TGX12
			MOV A,TGX12
			CJNE A,#91,NT23
				MOV TGX12,#5
		NT23:
			MOV TG_CHINH,TGX12
	TNT2:
		CALL TINH_TG
		JMP TNT
;--------------------------------------- 
NT3:
	CJNE CHINH,#4,NT4
		CJNE CHE_DO,#1,NT31
			INC TGV11
			MOV A,TGV11
			CJNE A,#16,NT30
				MOV TGV11,#1
		NT30:
			MOV TG_CHINH,TGV11
			JMP TNT3
	NT31:
		CJNE CHE_DO,#2,TNT3
			INC TGV12
			MOV A,TGV12
			CJNE A,#16,NT33
				MOV TGV12,#1
		NT33:
			MOV TG_CHINH,TGV12
	TNT3:
		CALL TINH_TG
		JMP TNT
;--------------------------------------- 
NT4:
	CJNE CHINH,#5,TNT
		CJNE CHE_DO,#1,NT41
			INC TGV21
			MOV A,TGV21
			CJNE A,#16,NT40
				MOV TGV21,#1
		NT40:
			MOV TG_CHINH,TGV21
			JMP TNT3
	NT41:
		CJNE CHE_DO,#2,TNT4
			INC TGV22
			MOV A,TGV22
			CJNE A,#16,NT43
				MOV TGV22,#1
		NT43:
			MOV TG_CHINH,TGV22
	TNT4:
		CALL TINH_TG
		JMP TNT
TNT:
	POP ACC ; LAY LAI GIA TRI CHO THANH GHI A TU NGAN XEP 
	RET
;======================================== 
NHAN_MODE: ; XU LY KHI NHAN MODE
	INC CHINH
	CJNE CHINH,#1,NM1
		CLR LED_CHINH ; BAT LED VAO CHINH KHI CHINH CHE DO
		MOV	8,CHE_DO ; XUAT HIEN THI CHE DO HIEN TAI
		JMP TNM
NM1:
	CJNE CHINH,#2,NM2
		MOV DEN_CHINH,#01111111B ; BAT DEN DO CHINH, KHI CHINH DEN DO
		MOV	8,#1 ;LUU MA 7 DOAN HANG DON VI THOI GIAN 1 VAO 61H
		CJNE CHE_DO,#1,NM11
			MOV TG_CHINH,TGD11 ; HIEN THI LEN THOI GIAN CHINH KHI CHINH, TUONG UNG VOI CHE DO
	NM11:
		CJNE CHE_DO,#2,NM2
			MOV TG_CHINH,TGD12	
;--------------------------------------- 
NM2:
	CJNE CHINH,#3,NM3
		MOV DEN_CHINH,#10111111B ; HIEN THI CHINH DEN XANH
		CJNE CHE_DO,#1,NM21
			MOV TG_CHINH,TGX11
	NM21:
		CJNE CHE_DO,#2,NM3
			MOV TG_CHINH,TGX12
;--------------------------------------- 
NM3:
	CJNE CHINH,#4,NM4
		MOV DEN_CHINH,#11011111B ; HIEN THI CHINH DEN VANG
		CJNE CHE_DO,#1,NM31
			MOV TG_CHINH,TGV11
	NM31:
		CJNE CHE_DO,#2,NM4
			MOV TG_CHINH,TGV12
;--------------------------------------- 
NM4:
	CJNE CHINH,#5,NM5
		MOV	8,#2
		CJNE CHE_DO,#1,NM41
			MOV TG_CHINH,TGV21
	NM41:
		CJNE CHE_DO,#2,NM5
			MOV TG_CHINH,TGV22
;--------------------------------------- 
NM5:
	SETB LED_CHINH
	CJNE CHINH,#6,NM6
		MOV TG_CHINH,#0
		MOV DEN_CHINH,#11111111B ; TAT HIEN THI CHINH
		MOV CHINH,#0 ; NAP CHINH LAI VE 0
		MOV	8,CHE_DO
		CALL NAPTG ; NAP THOI GIAN CHINH VAO 24C04
		
NM6:
	CJNE CHE_DO,#0,NM7 ; KIEM TRA CHE DO = 0
		CJNE CHINH,#2,NM7 ; KIEM TRA CHINH = 2, GIOI HAN CHINH TU 0-1 KHI CHE DO = 0
			MOV DEN_CHINH,#11111111B ; BAT DEN DO CHINH, KHI CHINH DEN DO
			MOV CHINH,#0 ; CHINH = 0 KHI CHE DO = 0
			MOV	8,CHE_DO
			CALL NAPTG ; NAP THOI GIAN CHINH VAO 24C04
NM7:
	CJNE CHE_DO,#3,TNM ; KIEM TRA CHE DO = 0
		CJNE CHINH,#2,TNM ; KIEM TRA CHINH = 2, GIOI HAN CHINH TU 0-1 KHI CHE DO = 0
			MOV DEN_CHINH,#11111111B ; BAT DEN DO CHINH, KHI CHINH DEN DO
			MOV CHINH,#0 ; CHINH = 0 KHI CHE DO = 0
			MOV	8,CHE_DO
			CALL NAPTG ; NAP THOI GIAN CHINH VAO 24C04
TNM:
	RET
;======================================== 
;CHUONG TRINH TAO THOI GIAN TRE 1 S CO GOI QUET LIEN TUC
TRE:
	PUSH 0
	MOV R0,#20 ;NAP SO LAN LAP 20 LAN
	MOV TMOD,#01h ;DAT CHE DO TIMER 0 16 BIT
LOOP2:
	MOV TH0,#HIGH(-49000) ;NAP GIA TRI CHO TIMER
	MOV TL0,#LOW(-49000) ;XAP SI 0,05 S
	SETB TR0 ;CHO PHEP TIMER HOAT DONG
QUET:
	CALL QUET_PHIM_NHAN
	CALL HIENTHI ;GOI CHUONG TRINH CON QUET LED HIEN THI
	JNB TF0,QUET ;LAP LAI TOI KHI TIMER TRAN
	CLR TR0 ;TAT TIMER
	CLR TF0 ;XOA CO BAO TRAN
	DJNZ R0,LOOP2 ; LAP LAI 20 LAN
	CPL BIT_GIAY
	POP 0
	RET
;====================
;VUNG DU LIEU
MA7DOAN:				;MA LED 7 DOAN
DB		03H,9FH,25H,0DH,99H,49H,41H,1EH,01H,09H,0FFH
;======================================== 
	SCL BIT P3.6 ;CHAN SCL NOI DEN ROM
	SDA BIT P3.7 ;CHAN SDA NOI DEN ROM					 

	W2404 EQU 10100000B ; 24C04 LENH GHI
	R2404 EQU 10100001B ; 24C04 LENH DOC
;======================================== 
ACK BIT 20H.7 ; BIT THUA NHAN
;======================================== 
NAPTG: ; NAP THOI GIAN VAO EEPROM
	CALL START_BIT ; STATR BIT
	MOV A,#W2404 ; NAP CHE DO GHI VAO EEPROM
	CALL SENT_BYTE ;GHI BYTE VAO TRC
	MOV	A,#0 ;NAP DIA CHI VAO RTC
	CALL SENT_BYTE
	MOV A,CHE_DO
	CALL SENT_BYTE ;GHI BYTE VAO EEPROM TU DONG TANG DIA CHI
	MOV	A,TGD11
	CALL SENT_BYTE
	MOV	A,TGX11
	CALL SENT_BYTE
	MOV	A,TGV11
	CALL SENT_BYTE
	MOV	A,TGV21
	CALL SENT_BYTE
	
	MOV	A,TGD12
	CALL SENT_BYTE
	MOV	A,TGX12
	CALL SENT_BYTE
	MOV	A,TGV12
	CALL SENT_BYTE
	MOV	A,TGV22
	CALL SENT_BYTE
	
	MOV	A,#0AAH
	CALL SENT_BYTE
	CALL STOP_BIT ;GOI STOP BIT
	RET ; TROAT KHOI CHUONG TRINH CON 
;======================================== 
DOC_TG: ;DOC THOI GIAN
	CALL START_BIT ; STATR BIT
	MOV	A,#W2404 ; NAP CHE DO GHI VAO EEPROM
	CALL SENT_BYTE ; GHI 1 BYTE
	MOV	A,#0 ; MAP DIA CHI BAN DAU = 0
	CALL SENT_BYTE
	CALL START_BIT ; LENH RESTART
	MOV A,#R2404  ; NAP CHE DO DOC VAO EEPROM 
	CALL SENT_BYTE
	CLR ACK ; XOA BIT THUA NHAN( CHO PHEP GHI LIEN TUC, DIA CHI TU DONG TANG )
	CALL READ_BYTE
	MOV CHE_DO,A
	
	CALL READ_BYTE
	MOV TGD11,A
	CALL READ_BYTE
	MOV TGX11,A
	CALL READ_BYTE
	MOV TGV11,A
	CALL READ_BYTE
	MOV TGV21,A
	
	CALL READ_BYTE
	MOV TGD12,A
	CALL READ_BYTE
	MOV TGX12,A
	CALL READ_BYTE
	MOV TGV12,A
	CALL READ_BYTE
	MOV TGV22,A
	SETB ACK ; DUNG CHE DO GHI LIEN TUC
	
	CALL READ_BYTE
	CALL STOP_BIT
	RET ; TROAT KHOI CHUONG TRINH CON 
;======================================== 
START_BIT: ; BIT START I2C
	SETB SDA ; DUA SDA LEN MUC CAO
	SETB SCL ;DUA CHAN SCL LEN MUC CAO
	NOP
	CLR SDA ; DUA SDA XUONG THAP
	CLR SCL ; DUA SCL XUONG THAP
	RET ; TROAT KHOI CHUONG TRINH CON 
;======================================== 
STOP_BIT: ; BIT STOP I2C			   	
	CLR	SDA	; DUA SDA XUONG THAP
	SETB SCL ; DUA CHAN SCL LEN MUC CAO
	NOP
	SETB SDA ; DUA SDA LEN MUC CAO
	RET ; TROAT KHOI CHUONG TRINH CON 
;========================================
ACK_BIT: ; DOC BIT THUA NHAN TU SLEVER
	SETB SDA ; DUA SDA LEN MUC CAO
	SETB SCL ; DUA CHAN SCL LEN MUC CAO
	NOP
	MOV C,SDA ; DOC BIT THUA NHAN
	MOV ACK,C ; NAP ACK BIT BIT THUA NHAN
	CLR	SCL ; DUA SCL XUONG THAP
	RET ; TROAT KHOI CHUONG TRINH CON 
;========================================
ACK_BIT_MASTER: ; TAO BIT THUA NHAN TU MASTER
	CLR SDA	; DUA SDA XUONG THAP
	SETB SCL ; DUA CHAN SCL LEN MUC CAO
	NOP
	CLR	SCL	; DUA SCL XUONG THAP
	SETB SDA ; DUA SDA LEN CAO
	RET ; TROAT KHOI CHUONG TRINH CON 
;========================================  
SENT_BYTE: ; GHI 1 BYTE DU LIEU
	PUSH 0 ; CAT TAM THOI GIA TRI THANH GHI R0 VAO NGAN XEP 
	MOV R0,#8 ; NAP SO LAN LAP 8 LAN
SB0:
	RLC A ; DICH TRAI CO CO C A, BIT CAO DUOC PHAT TRUOC
	MOV SDA,C ; PHAI BIT RA SDA
	SETB SCL ; DUA SCL LEN MUC CAO, TAO  XUNG NHIP
	NOP
	CLR SCL	; DUA SCL XUONG THAP
	DJNZ R0,SB0 ; LAP LAI DU 8 LAN
	CALL ACK_BIT ; DOC BIT THUA NHAN
	POP 0 ;LAY GIA TRI CHO THANH GHI R0 TU NGAN XEP 
	RET ; TRO VE CHUONG TRINH GOI NO
;======================================== 
READ_BYTE: ; DOC 1 BYTE DU LIEU
	PUSH 0 ; CAT TAM GIA TRI THANH GHI R0 VAO NGAN XEP				 	
	MOV R0,#8 ; NAP SO LAN LAP 8 LAN CHO 8 BIT
RB0:
	SETB SCL ; DUA CHAN SCL LEN MUC CAO
	MOV C,SDA ; DOC BIT TU CHAN SDA
	CLR SCL	; DUA SCL XUONG THAP
	RLC A ; DICH TRAI CO CO A, NHAN BYTE CAO TRUOC
	DJNZ R0,RB0 ; LAP LAI DU 8 BIT
	JB ACK,RB1 ; THUA NHAN TU MASTER HAY KHONG THUA NHAN
	CALL ACK_BIT_MASTER	; THUA NHAN TU MASTER KHI ACK = 0, DOC LIEN TIEP DU LIEU
	JMP RB2
RB1:
	CALL ACK_BIT ; KHONG THUA NHAN TU MASTER, KET THUC DOC
RB2:
	POP 0 ;LAY LAI GIA TRI CHO THANH GHI R0 TU NGAN XEP 
	RET
;====================
END